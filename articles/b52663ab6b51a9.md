---
title: "個人的に面食らったJulia言語動作集(仮)"
emoji: "😲"
type: "tech"
topics:
  - "julia"
published: false
---

# はじめに
Julia言語をやっていて、予想と違った動作に出会うことがあります。雑多な内容になりそうですが、本記事では私が驚かされた実行結果などを記載していきます。
他にも面白い例があればコメント歓迎です！

本記事の実行環境は以下です：
```julia
julia> versioninfo()
Julia Version 1.7.3
Commit 742b9abb4d (2022-05-06 12:58 UTC)
Platform Info:
  OS: Linux (x86_64-pc-linux-gnu)
  CPU: AMD Ryzen 7 2700X Eight-Core Processor
  WORD_SIZE: 64
  LIBM: libopenlibm
  LLVM: libLLVM-12.0.1 (ORCJIT, znver1)
```

# 雑多な例たち

以下の例の多くは、言語仕様上意図的に決められたものだと思います。
一部は将来的にJulia 2.0で修正される部分もあるかも知れませんが、「コーナーケースを通じて言語仕様の気持ちを理解する」ことが出来れば嬉しいです。

## 乗法演算子の省略
Juliaでは乗法演算子`*`を省略できることがありますが、指数表記の方が優先されます。

```julia
julia> e1, f1, g1 = 2,3,4
(2, 3, 4)

julia> 5e1
50.0

julia> 5f1
50.0f0

julia> 5g1
20
```

シンタックスハイライタが優秀なら気づけますね。下記のように下付き文字を使って回避することもできます：

```julia
julia> e₁, f₁, g₁ = 2,3,4
(2, 3, 4)

julia> 5e₁
10

julia> 5f₁
15

julia> 5g₁
20
```

## falseでの掛け算

JuliaではBoolean型は整数型の一種として扱われ、`true`/`false`はそれぞれ`1`/`0`に対応します。

```julia
julia> false isa Bool <: Integer <: Number
true

julia> Int[false, true]
2-element Vector{Int64}:
 0
 1
```

しかし、`false`は`0`と異なる挙動を示すことがあります：

```julia
julia> NaN * 0
NaN

julia> Inf * 0
NaN

julia> NaN * false
0.0

julia> Inf * false
0.0
```

これは[strong zeroと呼ばれるもの](https://docs.julialang.org/en/v1/manual/mathematical-operations/#Arithmetic-Operators)[^1]で、掛け算の相手が`NaN`とか`Inf`であっても強制的に`0`を返してくれます。ときに便利ですが、演算の順序には気をつける必要があります：

```julia
julia> false * NaN * NaN
NaN

julia> NaN * false * NaN
NaN

julia> NaN * NaN * false
0.0
```

[^1]: force zeroと呼ぶ方が韻が踏めて覚えやすいと思いますが、そのような表記はされないようです。英語ムズカシイ

## 推移的でない等号

`LinearAlgebra.I`は単位行列を表すもので`I(3)`は3×3の単位行列を返します。
`I`と`I(3)`は等しいですが，`I(3)`と`I(4)`は等しくありません。

```julia
julia> using LinearAlgebra

julia> I(3)
3×3 Diagonal{Bool, Vector{Bool}}:
 1  ⋅  ⋅
 ⋅  1  ⋅
 ⋅  ⋅  1

julia> I(3) == I == I(4)
true

julia> I(3) == I(4)
false
```

## 指数演算

組み込み関数`^`はかなり特殊です。`^(a,b)`は型不安定なように見えますが、これはparsingのときに解消されており、実行時には型安定になっています。

```julia
julia> ^(2,3)
8

julia> ^(2,-3)
0.125

julia> g = ^
^ (generic function with 69 methods)

julia> g(2,3)
8

julia> g(2,-3)
ERROR: DomainError with -3:
Cannot raise an integer x to a negative power -3.
Make x or -3 a float by adding a zero decimal (e.g., 2.0^-3 or 2^-3.0 instead of 2^-3), or write 1/x^3, float(x)^-3, x^float(-3) or (x//1)^-3
Stacktrace:
 [1] throw_domerr_powbysq(#unused#::Int64, p::Int64)
   @ Base ./intfuncs.jl:245
 [2] power_by_squaring(x_::Int64, p::Int64)
   @ Base ./intfuncs.jl:266
 [3] ^(x::Int64, p::Int64)
   @ Base ./intfuncs.jl:290
 [4] top-level scope
   @ REPL[5]:1
```

どのようなメソッドが呼び出されているかを調べるには`@code_lowered`が便利です。

```julia
julia> f1(x) = 2^x
f1 (generic function with 1 method)

julia> f2(x) = x^2
f2 (generic function with 1 method)

julia> f1(3), f2(3)
(8, 9)

julia> @code_lowered f1(3)
CodeInfo(
1 ─ %1 = 2 ^ x
└──      return %1
)

julia> @code_lowered f2(3)
CodeInfo(
1 ─ %1 = Core.apply_type(Base.Val, 2)
│   %2 = (%1)()
│   %3 = Base.literal_pow(Main.:^, x, %2)
└──      return %3
)
```

`f1`では型安定な`^`が呼ばれており、`f2`では`Base.literal_pow`が呼ばれていることが確認できます。

## Numberがiterable
Numberは0次元配列と同様にiterableです。

```julia
julia> 3 == 3[1]
true

julia> 3[1][1]
3

julia> [i for i in 3]
0-dimensional Array{Int64, 0}:
3

julia> size(3)
()

julia> reshape([3])
0-dimensional Array{Int64, 0}:
3

julia> reshape([3]) == 3
false
```

これの議論については、例えば https://github.com/JuliaLang/julia/issues/7903 などを見ると良いそうです。

## NaNの型

`NaN`は Not a Number の略ですが、`Number`です。

```julia
julia> NaN isa Number  # Not a number
true
```

## パスの結合

## ゼロ
```julia
julia> zero(Real)
0

julia> zeros(3)
3-element Vector{Float64}:
 0.0
 0.0
 0.0
```

## 行列とベクトルの積
「`(行列)*(縦ベクトル)`は`(縦ベクトル)`になるべき」、つまり「`M isa AbstractMatrix`かつ`v isa AbstractVector`のとき、`M*v`は`AbstractVector`になるべき」だと思いますが、一般には成立しません。

```julia
julia> M = [1,2,3]'
1×3 adjoint(::Vector{Int64}) with eltype Int64:
 1  2  3

julia> v = [1,2,3]
3-element Vector{Int64}:
 1
 2
 3

julia> M isa AbstractMatrix
true

julia> v isa AbstractVector
true

julia> M*v isa AbstractVector
false
```

詳しくは[Juliaの行列・ベクトルを完全に理解すっぞ！！](https://zenn.dev/hyrodium/articles/3fa3882e4bca04)をどうぞ

## 大小比較
「小なり」には3種類あって、それぞれ挙動が異なります。

* `isless` : 厳格に大小比較
* `<` : (数学的に)等しいなら小なりでない
* `≤` : `<`に等号も含めちゃう

```julia
julia> isless(-0.0, 0.0), -0.0 < 0.0, -0.0 ≤ 0.0
(true, false, true)

julia> isless(0.0, 0.0), 0.0 < 0.0, 0.0 ≤ 0.0
(false, false, true)
```

`isless`は`sort`に便利です：

```julia
julia> sort([-1.0, 0.0, -0.0, 2.0])
4-element Vector{Float64}:
 -1.0
 -0.0
  0.0
  2.0

julia> sort([-1.0, 0.0, -0.0, 2.0], lt = <)
4-element Vector{Float64}:
 -1.0
  0.0
 -0.0
  2.0
```

しかし、`searchsorted`関数(第2引数と等しいrangeを返すもの)に対しては注意が必要です。

```julia
julia> searchsorted([-1.0, -0.0, 0.0, 2.0, 2.0, 3.0], 2)
4:5

julia> [-1.0, -0.0, 0.0, 2.0, 2.0, 3.0][ans]
2-element Vector{Float64}:
 2.0
 2.0

julia> searchsorted([-1.0, -0.0, 0.0, 2.0, 2.0, 3.0], 0)
3:3

julia> searchsorted([-1.0, -0.0, 0.0, 2.0, 2.0, 3.0], -0)
3:3

julia> searchsorted([-1.0, -0.0, 0.0, 2.0, 2.0, 3.0], 0.0)
3:3

julia> searchsorted([-1.0, -0.0, 0.0, 2.0, 2.0, 3.0], -0.0)
2:2
```

これを回避するには`lt = <`を指定すればOKです。

```julia
julia> searchsorted([-1.0, -0.0, 0.0, 2.0, 2.0, 3.0], 0.0, lt = <)
2:3

julia> searchsorted([-1.0, -0.0, 0.0, 2.0, 2.0, 3.0], -0.0, lt = <)
2:3
```

## `Vararg`は型ではない


## VariantとCovariant

```julia
julia> (3,4) isa Tuple{Real,Real}
true

julia> [3,4] isa Vector{Real}
false

julia> [3,4] isa Vector{<:Real}
true

julia> (3,4) isa Tuple{<:Real,<:Real}
true
```

## スペースと行列
一般に、二項演算子の前後のスペースは無視されますが、行列記法の場合には列の区切りと衝突します。

```julia
julia> [1 2]
1×2 Matrix{Int64}:
 1  2

julia> [1 -2]
1×2 Matrix{Int64}:
 1  -2

julia> [1 - 2]
1-element Vector{Int64}:
 -1

julia> [1 (-) 2]
1×3 Matrix{Any}:
 1  -  2

julia> [1 (-)(2)]
1×2 Matrix{Int64}:
 1  -2
```

## 型の包含関係は集合としての包含関係ではない
Juliaは科学技術計算に強い言語で、一方で型の間には包含関係があります。


しかしこの包含関係`<:`は集合としての包含関係ではありません。


例えば、二重数に関しては..
